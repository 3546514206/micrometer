description 'Instrumentation of components using Micrometer'

dependencies {
    api project(':micrometer-core')

    // instrumentation options
    optionalApi 'io.dropwizard.metrics:metrics-core'

    // cache monitoring
    optionalApi 'com.google.guava:guava'
    optionalApi 'com.github.ben-manes.caffeine:caffeine'
    optionalApi 'net.sf.ehcache:ehcache'
    optionalApi 'javax.cache:cache-api'
    optionalApi 'com.hazelcast:hazelcast'
    optionalApi 'org.hibernate:hibernate-entitymanager'

    // server runtime monitoring
    optionalApi 'org.eclipse.jetty:jetty-server'
    optionalApi 'org.eclipse.jetty:jetty-client'
    optionalApi 'org.apache.tomcat.embed:tomcat-embed-core'
    optionalApi 'org.glassfish.jersey.core:jersey-server'
    optionalApi 'io.grpc:grpc-api'

    // apache httpcomponents monitoring
    optionalApi 'org.apache.httpcomponents:httpclient'
    optionalApi 'org.apache.httpcomponents:httpasyncclient'

    // hystrix monitoring
    optionalApi 'com.netflix.hystrix:hystrix-core'

    // log monitoring
    optionalApi 'ch.qos.logback:logback-classic'
    optionalApi 'org.apache.logging.log4j:log4j-core'

    // @Timed AOP
    optionalApi 'org.aspectj:aspectjweaver'

    optionalApi 'com.squareup.okhttp3:okhttp'

    optionalApi 'org.mongodb:mongodb-driver-sync'

    optionalApi 'org.jooq:jooq'

    optionalApi 'org.apache.kafka:kafka-clients'
    optionalApi 'org.apache.kafka:kafka-streams'

    testImplementation project(':micrometer-test')

    testImplementation 'io.projectreactor:reactor-test'

    // JUnit 5
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'com.tngtech.archunit:archunit-junit5'

    testImplementation 'org.jsr107.ri:cache-ri-impl'

    // We have tests for the many features that are optional dependencies, so add
    // them here just like a user would need to.
    testImplementation 'com.google.guava:guava'
    testImplementation 'com.github.ben-manes.caffeine:caffeine'
    testImplementation 'net.sf.ehcache:ehcache'
    testImplementation 'javax.cache:cache-api'
    testImplementation 'com.hazelcast:hazelcast'

    testImplementation 'org.mockito:mockito-inline'

    testImplementation 'org.hsqldb:hsqldb'

    // dependency injection tests
    testImplementation 'javax.inject:javax.inject'
    testImplementation 'org.springframework:spring-context'
    testImplementation 'com.google.inject:guice'

    testImplementation 'com.h2database:h2'

    // Uncomment these if you are interested in testing injection with dagger in MeterRegistryInjectionTest
//    testImplementation 'com.google.dagger:dagger'
//    testAnnotationProcessor 'com.google.dagger:dagger-compiler'

    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.awaitility:awaitility'

    testImplementation 'org.ehcache:ehcache'

    testImplementation 'org.apache.kafka:kafka-clients'

    testImplementation 'org.apache.commons:commons-pool2:2.+'

    testImplementation 'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-inmemory'
    testRuntimeOnly 'org.glassfish.jersey.inject:jersey-hk2'

    testImplementation 'ru.lanwen.wiremock:wiremock-junit5'
    testImplementation 'com.github.tomakehurst:wiremock-jre8-standalone'

    // Log4j2 Async dependency
    testImplementation 'com.lmax:disruptor:3.4.+'

    // Kafka binder IT dependencies
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:kafka'

    // Postgres Binder IT dependencies
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.postgresql:postgresql'

    testImplementation 'org.testcontainers:mongodb'
}

task shenandoahTest(type: Test) {
    // set heap size for the test JVM(s)
    maxHeapSize = "1500m"

    useJUnitPlatform {
        includeTags 'gc'
    }

    jvmArgs '-XX:+UseShenandoahGC'
}

task zgcTest(type: Test) {
    // set heap size for the test JVM(s)
    maxHeapSize = "1500m"

    useJUnitPlatform {
        includeTags 'gc'
    }

    jvmArgs '-XX:+UseZGC'
}

task openj9BalancedTest(type: Test) {
    // set heap size for the test JVM(s)
    maxHeapSize = "1500m"

    useJUnitPlatform {
        includeTags 'gc'
    }

    jvmArgs '-Xgcpolicy:balanced'
}

task openj9ConcurrentScavengeTest(type: Test) {
    // set heap size for the test JVM(s)
    maxHeapSize = "1500m"

    useJUnitPlatform {
        includeTags 'gc'
    }

    jvmArgs '-Xgc:concurrentScavenge'
}
